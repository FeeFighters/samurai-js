# Samurai Payment Forms Module
# ------------------------
$ = Samurai.jQuery

# Module for performing ajax payment transactions
@module "Samurai", ->
  @Payments = do ->
    rpc = null  # our easyXDM socket
    remote = 'https://api.samurai.feefighters.com/provider.html'
    transparentRedirectUrl = 'https://api.samurai.feefighters.com/v1/payment_methods/tokenize.json'

    init = ->
      transparentRedirectUrl = <%= Rails.env.production? ? "'https://api.samurai.feefighters.com/v1/payment_methods/tokenize.json'" :
        %{if Samurai.config.mock then "/uber_gateway_mock/v1/payment_methods" else "https://api.samurai.feefighters.com/v1/payment_methods/tokenize.json"} %> # API endpoint
      remote = <%= Rails.env.production? ? "'https://api.samurai.feefighters.com/provider.html'" :
      %{if Samurai.config.mock then "/provider.html" else "https://api.samurai.feefighters.com/provider.html"} %>

      preparePaymentForms()
      setupRpc() unless Samurai.config.withoutPayments  # initialize the easyXDM framework
      attachPaymentFormHandlers()  # attach event handlers to any payment forms

      # Expose the payment method on the Samurai object, for convenience
      Samurai.payment = payment

    # Setup the easyXDM socket
    setupRpc = ->
      Samurai.log 'Setting up RPC Provider'
      rpc = new easyXDM.Rpc {
        remote: remote
      }, { remote: { request: {} }}

    # This is the main payment api call
    # It will generate a `payment_method_token` based on the supplied credit card data,
    # and pass it to the callback function
    payment = (data, callback) ->
      rpc.request {
        url: transparentRedirectUrl
        method: 'POST'
        data: prepPaymentData data
        dataType: 'json'
        headers:
          'Accept': 'application/json'
      }, (response) ->
        json = $.parseJSON response.data
        Samurai.log 'payment response'
        Samurai.log json
        callback(json)

    # Add the extra data fields that we need to add, ie: `redirect_url`
    prepPaymentData = (data) ->
      data.redirect_url = 'http://location/'
      data.merchant_key = Samurai.config.merchant_key
      data.sandbox = Samurai.config.sandbox
      data.ignore_merchant_key = true if Samurai.config.mock?
      data

    preparePaymentForms = ->
      # Disable the submit button, while load the rest of the payments stack
      Samurai.log 'Disabling payment forms while loading setupRPC'
      $('form[data-samurai-ajax=""] button[type="submit"]').attr('disabled', true).text('Loading...')

    # Attach to any forms with `data-samurai-ajax`, and make them ajax-aware
    attachPaymentFormHandlers = ->
      Samurai.log 'Attaching payment form handlers, and re-enabling submit buttons'

      samurai = this
      $('form[data-samurai-ajax=""]').each ->

        # Re-enable the submit buttons
        $(this).children('button[type="submit"]').attr('disabled', false).text('Submit Payment')

        # Attach a default payment error renderer if the
        # `data-samurai-disable-error-rendering` attribute isn't set and
        # provided we haven't already done so
        
        if Samurai.PaymentErrorRenderer and
           !$(this).attr('data-samurai-disable-error-rendering')?

          Samurai.PaymentErrorRenderer.forForm(this)

        # On submit:
        #
        # * stop event propagation
        # * trigger the `loading` event
        # * display the `.loading` spinner, and change the button style to inactive
        # * call `Samurai.payment()` to submit the credit card data
        #   * in the callback function, trigger the `payment` event on the form
        $(this).submit (event) ->
          event.preventDefault()
          $(this).trigger('loading').addClass('loading')
          $(this).children('.loading').show()
          $(this).children('button[type="submit"]').attr('disabled', true)
          payment $(this).formParams(false), (data) =>
            $(this).trigger('payment', data)
          false

        # Bind to the `errors-shown` event and do the following if the payment
        # information was invalid:
        #
        # * hide the `.loading` spinner
        # * enable the submit button
        # * change the label of the submit button to "Resubmit Payment"
        $(this).bind 'errors', (event) ->
          $(this)
            .removeClass('loading')
            .children('.loading').hide().end()
            .children('button[type="submit"]')
              .attr('disabled', false).text('Resubmit Payment')

        # Bind to the `completed` event:
        #
        # * hide the button (only let them purchase once)
        # * set the `.loading` class on the form
        # * hide the `.loading` spinner
        # * show the `.results` container
        $(this).bind 'completed', (event) ->
          $(this).children('button[type="submit"]').hide()
          $(this).removeClass('loading').addClass('complete')
          $(this).children('.loading').hide()
          $(this).children('.results').show()

    # Export public API
    {init, payment}


